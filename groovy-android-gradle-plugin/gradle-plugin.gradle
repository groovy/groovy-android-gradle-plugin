/*
 * Copyright 2017 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'groovy'
apply plugin: 'maven'

apply from: "$rootDir/gradle/publishing.gradle"
apply from: "$rootDir/gradle/bintray.gradle"
apply from: "$rootDir/gradle/artifactory.gradle"
apply from: "$rootDir/gradle/versionFile.gradle"
apply from: "$rootDir/gradle/codenarc.gradle"
apply from: "$rootDir/gradle/publishPlugins.gradle"

description = 'Groovy support for Android with the Gradle build tool - http://www.groovy-lang.org'

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

dependencies {
  implementation localGroovy()
  implementation gradleApi()

  implementation "com.android.tools.build:gradle:$androidPluginVersion"

  testImplementation gradleTestKit()
  testImplementation "com.android.tools.build:gradle:3.0.0"
  testImplementation "org.spockframework:spock-core:$spockVersion", {
    exclude group: 'org.codehaus.groovy', module: 'groovy-all'
  }
}

task fullTest(type: Test) {
  description = 'Run all tests - this will run all compilation specs with all the different android plugin versions'
  group = 'Verification'
  dependsOn check
  systemProperty 'allTests', true
}

task release {
  description = 'Releases a version of the plugin on Artifactory and Bintray'
  group = 'Publishing'
  dependsOn fullTest, artifactoryPublish, bintrayUpload, publishPlugins, rootProject.tasks.tagRelease
}

tasks.withType(Test) {
  doFirst {
    maxParallelForks 1
  }

  systemProperty 'androidPluginVersion', androidPluginVersion
  systemProperty 'buildToolsVersion', buildToolsVersion
  systemProperty 'compileSdkVersion', compileSdkVersion
  systemProperty 'kotlinVersion', kotlinVersion

  environment "GRADLE_USER_HOME", gradle.gradleUserHomeDir

  dependsOn install

  testLogging {
    exceptionFormat 'full'
  }
}
