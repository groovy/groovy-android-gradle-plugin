/*
 * Copyright 2016 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.2.0'
    classpath 'com.android.tools.build:gradle:2.1.2'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
    //noinspection GradleDynamicVersion (this gets replaced by androidGroovyLocal.gradle)
    classpath 'org.codehaus.groovy:gradle-groovy-android-plugin:+'
  }
}

apply from: "$rootDir/gradle/idea/idea.gradle"

allprojects {
  version = '1.0.1-SNAPSHOT'
  group = 'org.codehaus.groovy'

  apply plugin: 'com.github.ben-manes.versions'
  apply from: "$rootDir/gradle/credentials.gradle"
}

subprojects {
  apply from: "$rootDir/gradle/projectLocalRepo.gradle"
  apply from: "$rootDir/gradle/androidGroovyLocal.gradle"

  repositories {
    jcenter()
  }
}

ext {
  androidPluginVersion = androidPluginVersion
  buildToolsVersion = '24.0.0'
  compileSdkVersion = 24
}

task tagRelease(type: Exec) {
  description = 'Tag the release and push it to github'
  group = 'Publishing'

  def tagString = "RELEASE_${version.replace('.', '_')}"
  commandLine 'bash', '-c',
"""
  git tag $tagString
  git push -q "https://$project.githubToken@github.com/groovy/groovy-android-gradle-plugin.git" $tagString > /dev/null 2>&1
"""
}

/**
 * Return the Android Gradle Plugin version for the buildscript so it may be used through out the project, and only
 * one place to set this value.
 */
String getAndroidPluginVersion() {
  return project.buildscript.configurations.classpath.resolvedConfiguration.firstLevelModuleDependencies.find {
    it.moduleGroup == 'com.android.tools.build' && it.moduleName == 'gradle'
  }.moduleVersion
}
